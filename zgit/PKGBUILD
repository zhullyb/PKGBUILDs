# Maintainer: Kevin Sanchez <kucashu@outlook.com>
# Maintainer: zhullyb <5435486@qq.com>
# Maintainer: DuckSoft <realducksoft@gmail.com>

pkgname=zgit
pkgver=0.7
pkgrel=1
pkgdesc="A fork of fgit-go, with the proxy of cloudflare worker"
arch=('x86_64')
url="https://github.com/zhullyb/fgit-go"
license=('GPL3')
depends=('git' 'glibc')
makedepends=('go')
provides=('zgit')
source=("$pkgname-$pkgver::git+$url.git")
sha512sums=('SKIP')

build() {
    cd "$srcdir"/$pkgname-$pkgver
    
    CGO_ENABLED=1 GO111MODULE=off go build -v \
        -buildmode=pie -trimpath \
        -ldflags="-s -w -X main.version=`cat version` -X 'main.timestamp=`date +'%Y-%m-%d %H:%M:%S'`' -extldflags=-Wl,-z,now,-z,relro" \
        -o zgit ./src/fgit.go
}

package() {
    cd "$srcdir"/$pkgname-$pkgver
    
    install -Dm755 -t "$pkgdir"/usr/bin/                ./zgit
    install -Dm644 -t "$pkgdir"/usr/share/doc/zgit   ./README.md
}

